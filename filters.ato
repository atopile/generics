import ElectricSignal

from "interfaces.ato" import Power
from "capacitors.ato" import Capacitor, CapacitorElectrolytic
from "inductors.ato" import Inductor
from "resistors.ato" import Resistor


module LowPassFilter:
    # Interfaces
    input = new ElectricSignal
    output = new ElectricSignal

    # Components
    capacitor = new Capacitor
    resistor = new Resistor

    # Default values
    capacitor.package = "0402"
    resistor.package = "0402"

    # Connections
    input.line ~ resistor.1; resistor.2 ~ output.line
    output.line ~ capacitor.1; capacitor.2 ~ output.reference.gnd

    input.reference ~ output.reference

    # Default values
    cutoff_frequency = 1kHz +/- 20%
    capacitor.value = 100nF +/- 10%

    assert 1 / (2 * 3.14 * resistor.value * capacitor.value) within cutoff_frequency


module LowPassPiFilter:
    C1 = new CapacitorElectrolytic
    L1 = new Inductor
    C2 = new CapacitorElectrolytic

    power_in = new Power
    power_out = new Power

    power_in.vcc ~ C1.anode; C1.anode ~ L1.p1
    power_out.vcc ~ L1.p2; L1.p2 ~C2.anode
    C1.cathode ~ power_in.gnd
    C2.cathode ~ power_out.gnd
    power_in.gnd ~ power_out.gnd
